# This file was specific to the Tableau Perforce setup, but contains
# some general approaches relevant to using Perforce workspaces

###
# Create a suffix 
#
alias p4='env CYGWIN=nontsec p4'
alias -s rb='_run_ruby'

# SPECIAL ZSH FUNCTION
#  NOTE: 'chpwd' is run at every directory change in a zsh shell,
#  so it's imperative that this function be fast
#
chpwd () {
  _z4_chpwd
  _set_ruby_bin
  _set_title
}

_set_title () {
  local new_title=zsh

  if [[ ! -z $z4_current_root ]]; then
    new_title=${z4_current_root##$DEVHOME/}
  fi
    
  /c/WINDOWS/system32/cmd.exe /c title $new_title
}

_run_ruby () {
  if (( $+commands[${1}] )); then
    `_get_ruby_bin`/ruby.exe `cygpath -w $commands[$1]` $*[2,-1]
  else
    `_get_ruby_bin`/ruby.exe `cygpath -w $1` $*[2,-1]
  fi
}

_get_ruby_bin () {
  local client=`z4get Client`
  if [[ "$client" == "$P4USER" ]]; then
    echo $ruby_bin_dir
  else
    if [[ "$client" == "$old_ruby_bin_client" ]]; then
      echo $old_ruby_bin_dir
      return 0
    fi

    old_ruby_bin_client=$client
    _set_ruby_bin
    echo $ruby_bin_dir
  fi
}

# Update $ruby_bin_dir according to the current client root, if it has a ruby
_set_ruby_bin () {
  _z4_get_current
  local new_ruby_bin=$z4_current_root/workgroup-support/ruby/bin
  if [[ -d $new_ruby_bin ]]; then
    old_ruby_bin_dir=$ruby_bin_dir
    ruby_bin_dir=$new_ruby_bin

    delete-from-path $old_ruby_bin_dir $ruby_bin_dir
  fi
}

# Create an alias for every *.rb command on the path that 
# is the command without the ".rb" extension.
for s in  ${(M)${^commands:t}:#*.rb}
do
    alias ${s%.rb}=$s
done
###

# Local Variables:
# mode: shell-script
# End:
