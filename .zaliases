#!/bin/zsh
# John Whitley's .zaliases
#
# Copyright (C) 2003-2012 John Whitley
#

# Define cd shortcut helper functions for standard directories
cdhelper src ~/src
cdhelper 3p ~/src/3p

cdhelper archive ~/Archive
cdhelper inbox ~/Inbox
cdhelper outbox ~/Outbox
cdhelper pending ~/Pending

rr () {
  local git_root=`git rev-parse --show-toplevel`
  cd $git_root/$1
}

_rr () {
  local git_root=`git rev-parse --show-toplevel`
  _path_files -/ -W $git_root
}
compdef _rr rr

# fuzzy-find cd using fzf
fd() {
  local dir
  dir=$(find ${1:-.} -path '*/\.*' -prune \
                  -o -type d -print 2> /dev/null | fzf +m) &&
  cd "$dir"
}

# wt -- change to an adjacent git working tree
function wt () {
  local toplevel
  toplevel=$(git rev-parse --show-toplevel 2> /dev/null)
  if [[ $? -gt 0 ]]; then
    echo "Not in a git repo"
    return
  fi

  local prefix=$(dirname "$toplevel")
  if [[ $# -gt 0 ]]; then 
    cd "$prefix/$1"
  else
    echo "No path passed; staying put"
  fi
}

# This assumes that all worktrees share a common parent directory
function _wt() {
  local toplevel
  toplevel=$(git rev-parse --show-toplevel 2> /dev/null)
  if [[ $? -gt 0 ]]; then
    _message "Not in a git repo"
    return
  fi

  local prefix=$(dirname "$toplevel")
 _path_files -/ -W "$prefix"
}

compdef _wt wt

# Abbreviation aliases
alias   b=bundle
alias   be='bundle exec'
alias   x=exit
alias   v=vcsh
alias   g=git
alias   s=ssh
alias	ipy=ipython

# skim aliases
# https://github.com/lotabout/skim
# repo skim -- 
alias   rsk='ffind | sk'
alias   vsk='v `rsk`'
alias   skb='ffind | sk --preview="bat {} --color=always"'
# Debian-ish environments install `bat` as `batcat`
alias_if_exist bat=batcat
# Ensure `bat` exists for use by `m`, below
weak_alias bat=less

# Fancy `more`-like alias
#   This name is purely historical, since I started using `more`
#   before `less` existed so my shorthand alias has always been `m`.
function m () {
  if [[ $# -eq 0 ]]; then
    less
  else
    bat $*
  fi
}

### Modify and/or override selected commands
unalias which >& /dev/null              # Always use which shell builtin

if [[ -n "`whence bcompare`" ]]; then
  function bc() { bcompare $@ }
fi
alias   lend='less +G'                  # less, starting at end of file
alias   lg=lazygit
alias   ls='ls -F'                      # ls always shows an indicator character (*/=@|)
alias   ll='ls -alh'                    # List long
alias   lsd='ll -d .[a-zA-Z0-9]*'       # List dot files and directories; don't expand dirs
alias   lse='ll (.)#*~'                 # List annoying emacs autosaves
alias   rme='rm -f (.)#*~'              # Remove annoying emacs autosaves
alias   j='jobs -l'                     # Show active jobs
alias   screen='screen -D -R'           # Attach here and now.  Power detach first, reconnect if possible.
alias   ri='ri -Tf ansi'                # Colorize ri output (ruby doc app)
function psf() { ps auxffwww | m }      # Show all running processes in wide tree format
alias   startx="startx >&- 2>&- &!"
alias   zcp='zmv -C'                    # Copy variant of the zmv utility
alias   zln='zmv -L'                    # Link variant of the zmv utility
function inboxmv() { mv '~/Desktop/Inbox/$1' $argv[2,-1] }
alias   path='echo $PATH'
alias   igrep='grep -i'
alias   pgrep='pgrep -i'
alias   st='git st'

# For use with [Exa](https://github.com/ogham/exa):
alias e=exa
alias el='exa -l'
alias elg='exa -l --git'

# DEPRECATED: on Mac hosts, invoking `ssh-add -A` is preferred, e.g.
# in ~/.zlocal/this/zenviron
#
# # Preface various commands with an attempt to connect to the ssh-agent
# ssh_commands=(git ssh scp sftp rsync startx stunnel svnc vcsh)
# auto-acquire-agent $ssh_commands

# ssh tunnel for synergy2 (http://synergy2.sourceforge.net/)
alias   ssyn="stunnel -R 24800"
# see also svnc in .zfunctons/svnc

# Aliases for new/synthetic commands
alias   rot13='tr N-ZA-Mn-za-m A-Za-z'
# Run zsh with the specified ZDOTDIR to test out changes
# to the rc files.
function ztest() { env ZDOTDIR=`print $1` zsh -il }

# Local Variables:
# mode: shell-script
# End:
