# John Whitley's .zenviron
# -- interactive shell environment settings
# Copyright (C) 2003-2012 John Whitley
#
# Based on an old .environ of 1985 lineage...

#
# TERMINAL SETUP
#

stty -ixon -ixoff                      # disable flow control

#
# mise setup. See https://mise.jdx.dev
#
# Used in lieu of individual tools such as rbenv and nodenv
#

if [[ -x ~/.local/bin/mise ]]; then
  eval "$(~/.local/bin/mise activate zsh)"
fi
#
# PROMPT SETUP
#

autoload -U promptinit; promptinit
prompt pure
export PURE_PROMPT_SYMBOL="❯❯"
export PURE_PROMPT_VICMD_SYMBOL="❮❯"
zstyle :prompt:pure:git:branch color '#f9bb07'
zstyle :prompt:pure:host color '#c1540e'
zstyle :prompt:pure:path color '#00837d'
zstyle :prompt:pure:prompt:error color '#dd312d'
zstyle :prompt:pure:prompt:success color '#159834'
zstyle :prompt:pure:username color '#c1540e'
zstyle :prompt:pure:vcsh color '#f9bb07'

#
# ZCHAIN SETUP -- ssh-agent utility
#

zchain-setup

#
# ENVIRONMENT -- PATH
#

# ---- BEGIN SETTINGS ----
#

# system path: Directories to prepend, append, and remove
 path_prepend=( $ZDOTDIR/local/$ZSHRC_OS/bin $ZDOTDIR/local/bin $ZDOTDIR/.local/bin \
                $path_prepend \
                /usr/local/bin /usr/local/sbin \
                /sbin /usr/sbin /usr/bin $SHELL:h )
  path_append=( $path_append )
path_unwanted=( . ~/bin ~ $path_unwanted )

#
# ---- END SETTINGS ----

## PATH SETUP

init-path path

# We can't add the python package-installed bin directory until after the
# main path is setup, so that we ask the correct python for sys.prefix:
if [[ ! -z `whence python3` ]]; then
    path=($path `python3 -c 'import sys; import os.path; print(os.path.join(sys.prefix,"bin"))'`)
fi

## INFOPATH SETUP
 infopath_prepend=( ~/local/share/info $infopath_prepend )
  infopath_append=( )
infopath_unwanted=( )

init-path infopath

### KEYBINDING SETUP
#

# The default KEYTIMEOUT is 40 (ms) which creates an intolerable delay when
# hitting ESC to get out of the vi insert mode.
#
# Too small a value (e.g. 1) makes the vim style surround bindings unusable.
 KEYTIMEOUT=10

# # Eliminate problems with hitting <ESC><ESC> when in viins mode.
# # The first <ESC> switches to vicmd, which then waits for follow-up input to
# # the <ESC> prefix, consuming the next keystroke.
# zle_noop () { }
# zle -N zle_noop
# bindkey -M vicmd '\e' zle_noop

# Make the zsh line editor (zle) respect '/' in paths as a word separator.
# Hallelujah!
export WORDCHARS=''

# Configure zsh-vim-mode cursors
MODE_CURSOR_VICMD="white block"
MODE_CURSOR_VIINS="green blinking bar"
MODE_CURSOR_SEARCH="green steady underline"

#
# ENVIRONMENT -- editor and pager setup
#
vim_order=( nvim vim vi )

for vim in $vim_order; do
    export VIM_EDITOR=`command -v $vim`
    [[ -n "$VIM_EDITOR" ]] && break
done

export EDITOR="$VIM_EDITOR"
export VISUAL=$EDITOR

# Don't override a platform-specific setting
weak_export GVIM_EDITOR=$VIM_EDITOR

export PAGER=`whence less`

# If we're in a git repo, use git ls-tree, otherwise use fd
export FZF_DEFAULT_COMMAND='(git ls-tree -r --name-only HEAD || \
  find . -path "*/\.*" -prune -o -type f -print -o -type l -print |
  sed s/^..//) 2> /dev/null'

export COFFEELINT_CONFIG=~/.coffeelint

# Less:
#   -i  ignore case in searches
#   -R  Display raw control characters, but try to keep track of screen appearance
export LESS='-iR'
#   Less input filter: script to auto-uncompress .Z, .gz., .bz2 files
lessfilter=~/local/bin/lesspipe.sh
if [[ -x $lessfilter ]]; then
    export LESSOPEN="| \"$lessfilter\" %s"
else
    echo ".zenviron could not find less filter script: $lessfilter"
fi

# Local Variables:
# mode: shell-script
# End:
# vim:ft=zsh:sw=4:sts=4:
